apply plugin: 'eclipse'


subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'findbugs'

    repositories {
        // superset of maven central
        jcenter()
    }

    sourceCompatibility = 1.8

    dependencies {
        // TODO: move test classes and change to testCompile
        compile 'junit:junit:4.12'
    }

    /* this prevents findbugs from being run every time the project is build, since the
     * analysis takes so long. To explicitly run findbugs, run "./gradlew findbugsAll"
     */
    check.dependsOn.remove("findbugsMain")
    check.dependsOn.remove("findbugsTest")

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
            html.stylesheet resources.text.fromFile("$rootProject.projectDir/config/xsl/findbugs-custom.xsl")
        }
    }

    findbugs {
        ignoreFailures = true
    }
}

project(':etomica-core') {
    
    dependencies {
        compile 'gov.nist.math:jama:1.0.3'
        compile 'org.apache.commons:commons-math3:3.3'
        compile files("$rootProject.projectDir/libs/ptolemy.jar")

        compile project(':etomica-graphics3D')
    }
}

project(':etomica-apps') {
    
    dependencies {
        compile 'com.googlecode.json-simple:json-simple:1.1'

        compile project(':etomica-core')
        compile project(':etomica-graph')
    }
}

project(':etomica-modules') {
    
    dependencies {
        compile project(':etomica-apps')
    }
}

project(':etomica-graph') {
    
    dependencies {
        compile 'xml-apis:xml-apis-ext:1.3.04'
        compile 'org.apache.xmlgraphics:batik-swing:1.7'
        compile 'org.apache.xmlgraphics:fop:1.0'
    }
}

task findbugsAll
subprojects { project -> findbugsAll.dependsOn("${project.path}:findbugsMain")}
